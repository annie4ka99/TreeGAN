start: syntax
syntax         : rule | rule syntax
rule           : opt_space LESS rule_name BIGGER opt_space ASSIGN_OP opt_space expression line_end
opt_space      : SPACE opt_space | SPACE?
mandatory_space: SPACE opt_space
expression     : list | list opt_space PIPE opt_space expression
line_end       : opt_space EOL | opt_space EOL line_end
list           : term | term mandatory_space list
term           : literal | LESS rule_name BIGGER
literal        : QUOT text1 QUOT | APOSTROPHE text2 APOSTROPHE
text1          : character1? | character1 text1
text2          : character2? | character2 text2
character      : letter | digit | symbol
letter         : "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
digit          : "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
symbol         :  "|" | " " | "!" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":" | ";" | ">" | "=" | "<" | "?" | "@" | "[" | "\\" | "]" | "^" | "_" | "`" | "{" | "}" | "~"
character1     : character | APOSTROPHE
character2     : character | QUOT
rule_name      : letter | rule_name rule_char
rule_char      : letter | digit | HYPHEN

LESS: "<"
BIGGER: ">"
ASSIGN_OP: "::="
SPACE: " "
PIPE: "|"
EOL: "\n"
HYPHEN: "-"
APOSTROPHE: "'"
QUOT: "\""
