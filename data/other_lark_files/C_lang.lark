start     : translation_unit
//        | preprocessing_file
//        | translation_unit start
//        | preprocessing_file start

// A.1 Lexical grammar
//  A.1.1 Lexical elements

token     : keyword
          | identifier
          | constant
          | string_literal
          | punctuator

preprocessing_token     : header_name
                        | identifier
                        | pp_number
                        | character_constant
                        | string_literal
                        | punctuator
                        | non_white_space_character

non_white_space_character     : "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o"
| "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I"
| "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "0" | "1" | "2"
| "3" | "4" | "5" | "6" | "7" | "8" | "9" | "!" | "\"" | "#" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "+" | "," | "-"
| "." | "/" | ":" | ";" | "<" | "=" | ">" | "?" | "@" | "[" | "\\" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~"

//  A.1.2 Keywords

keyword     : "auto" | "break" | "case" | "char" | "const" | "continue" | "default" | "do" | "double" | "else" | "enum"
| "extern" | "float" | "for" | "goto" | "if" | "inline" | "int" | "long" | "register" | "restrict" | "return" | "short"
| "signed" | "sizeof" | "static" | "struct" | "switch" | "typedef" | "union" | "unsigned" | "void" | "volatile"
| "while" | "_Alignas" | "_Alignof" | "_Atomic" | "_Bool" | "_Complex" | "_Generic" | "_Imaginary" | "_Noreturn"
| "_Static_assert" | "_Thread_local"

//  A.1.3 Identifiers

identifier_opt     : identifier?

identifier     : identifier_nondigit
               | identifier identifier_nondigit
               | identifier digit

identifier_nondigit     : nondigit
                        | universal_character_name
// other implementation-defined characters

nondigit     : "-" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q"
| "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K"
| "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

digit     : "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

//  A.1.4 Universal character names

universal_character_name     : "\\u" hex_quad
                             | "\\U" hex_quad hex_quad

hex_quad     : hexadecimal_digit hexadecimal_digit hexadecimal_digit hexadecimal_digit

//  A.1.5 Constants

constant     : integer_constant
             | floating_constant
             | enumeration_constant
             | character_constant

integer_constant     : decimal_constant integer_suffix_opt
                     | octal_constant integer_suffix_opt
                     | hexadecimal_constant integer_suffix_opt

decimal_constant     : nonzero_digit
                     | decimal_constant digit

octal_constant     : "0"
                   | octal_constant octal_digit

hexadecimal_constant     : hexadecimal_prefix hexadecimal_digit
                         | hexadecimal_constant hexadecimal_digit

hexadecimal_prefix     : "0x" | "0X"

nonzero_digit     : "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

octal_digit     : "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7"

hexadecimal_digit     : "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
                      | "a" | "b" | "c" | "d" | "e" | "f" | "A" | "B" | "C" | "D" | "E" | "F"

integer_suffix_opt     : integer_suffix?

integer_suffix     : unsigned_suffix long_suffix_opt
                   | unsigned_suffix long_long_suffix
                   | long_suffix unsigned_suffix_opt
                   | long_long_suffix unsigned_suffix_opt

unsigned_suffix_opt     : unsigned_suffix?

unsigned_suffix     : "u" | "U"

long_suffix_opt     : long_suffix?

long_suffix     : "l" | "L"

long_long_suffix     : "ll" | "LL"

floating_constant     : decimal_floating_constant
                      | hexadecimal_floating_constant

decimal_floating_constant     : fractional_constant exponent_part_opt floating_suffix_opt
                              | digit_sequence exponent_part floating_suffix_opt

hexadecimal_floating_constant     : hexadecimal_prefix hexadecimal_fractional_constant binary_exponent_part floating_suffix_opt
                                  | hexadecimal_prefix hexadecimal_digit_sequence binary_exponent_part floating_suffix_opt

fractional_constant     : digit_sequence_opt "." digit_sequence
                        | digit_sequence "."

exponent_part_opt     : exponent_part?

exponent_part     : "e" sign_opt digit_sequence
                  | "E" sign_opt digit_sequence

sign_opt     : sign?

sign     : "+" | "-"

digit_sequence_opt     : digit_sequence?

digit_sequence     : digit
                   | digit_sequence digit

hexadecimal_fractional_constant     : hexadecimal_digit_sequence_opt "." hexadecimal_digit_sequence
                                    | hexadecimal_digit_sequence "."

binary_exponent_part     : "p" sign_opt digit_sequence
                         | "P" sign_opt digit_sequence

hexadecimal_digit_sequence_opt     : hexadecimal_digit_sequence?

hexadecimal_digit_sequence     : hexadecimal_digit
                               | hexadecimal_digit_sequence hexadecimal_digit

floating_suffix_opt     : floating_suffix?

floating_suffix     : "f" | "l" | "F" | "L"

enumeration_constant     : identifier

character_constant     : "'" c_char_sequence "'"
                       | "L'" c_char_sequence "'"
                       | "u'" c_char_sequence "'"
                       | "U'" c_char_sequence "'"

c_char_sequence     : c_char
                    | c_char_sequence c_char

c_char     : "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r"
| "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L"
| "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "0" | "1" | "2" | "3" | "4" | "5"
| "6" | "7" | "8" | "9" | " " | "!" | "\"" | "#" | "$" | "%" | "&" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":"
| ";" | "<" | "=" | ">" | "?" | "@" | "[" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~" | escape_sequence

escape_sequence     : simple_escape_sequence
                    | octal_escape_sequence
                    | hexadecimal_escape_sequence
                    | universal_character_name

simple_escape_sequence     : "\\'" | "\\\"" | "\\?" | "\\\\" | "\\a" | "\\b" | "\\f" | "\\n" | "\\r" | "\\t" | "\\v"

octal_escape_sequence     : "\\" octal_digit
                          | "\\" octal_digit octal_digit
                          | "\\" octal_digit octal_digit octal_digit

hexadecimal_escape_sequence     : "\\x" hexadecimal_digit
                                | hexadecimal_escape_sequence hexadecimal_digit

//  A.1.6 String literals

string_literal     : encoding_prefix_opt "\"" s_char_sequence_opt "\""

encoding_prefix_opt     : encoding_prefix?

encoding_prefix     : "u8" | "u" | "U" | "L"

s_char_sequence_opt     : s_char_sequence?

s_char_sequence     : s_char
                    | s_char_sequence s_char

s_char     : "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r"
| "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L"
| "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "0" | "1" | "2" | "3" | "4" | "5"
| "6" | "7" | "8" | "9" | " " | "!" | "#" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":"
| ";" | "<" | "=" | ">" | "?" | "@" | "[" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~" | escape_sequence

//  A.1.7 Punctuators

punctuator     : "[" | "]" | "(" | ")" | "{" | "}" | "." | "->" | "++" | "--" | "&" | "*" | "+" | "-" | "~" | "!" | "/"
| "%" | "<<" | ">>" | "<" | ">" | "<=" | ">=" | "==" | "!=" | "^" | "|" | "&&" | "||" | "?" | ":" | ";" | "..." | "="
| "*=" | "/=" | "%=" | "+=" | "-=" | "<<=" | ">>=" | "&=" | "^=" | "|=" | "," | "#" | "##" | "<:" | ":>" | "<%" | "%>"
| "%:" | "%:%:"

//  A.1.8 Header names

header_name     : "<" h_char_sequence ">"
                | "\"" q_char_sequence "\""

h_char_sequence     : h_char
                    | h_char_sequence h_char

h_char     : "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r"
| "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L"
| "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "0" | "1" | "2" | "3" | "4" | "5"
| "6" | "7" | "8" | "9" | " " | "!" | "\"" | "#" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/"
| ":" | ";" | "<" | "=" | "?" | "@" | "[" | "\\" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~"

q_char_sequence     : q_char
                    | q_char_sequence q_char

q_char     : "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r"
| "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L"
| "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" | "0" | "1" | "2" | "3" | "4" | "5"
| "6" | "7" | "8" | "9" | " " | "!" | "#" | "$" | "%" | "&" | "'" | "(" | ")" | "*" | "+" | "," | "-" | "." | "/" | ":"
| ";" | "<" | "=" | ">" | "?" | "@" | "[" | "\\" | "]" | "^" | "_" | "`" | "{" | "|" | "}" | "~"

//  A.1.9 Preprocessing numbers

pp_number     : digit
              | "." digit
              | pp_number digit
              | pp_number identifier_nondigit
              | pp_number "e" sign
              | pp_number "E" sign
              | pp_number "p" sign
              | pp_number "P" sign
              | pp_number "."

//- A.2 Phrase structure grammar
//  A.2.1 Expressions

primary_expression     : identifier
                       | constant
                       | string_literal
                       | "(" expression ")"
                       | generic_selection

generic_selection     : "_Generic(" assignment_expression "," generic_assoc_list ")"

generic_assoc_list     : generic_association
                       | generic_assoc_list "," generic_association

generic_association     : type_name ":" assignment_expression
                        | "default:" assignment_expression

postfix_expression     : primary_expression
                       | postfix_expression "[" expression "]"
                       | postfix_expression "(" argument_expression_list_opt ")"
                       | postfix_expression "." identifier
                       | postfix_expression "->" identifier
                       | postfix_expression "++"
                       | postfix_expression "--"
                       | "(" type_name "){" initializer_list "}"
                       | "(" type_name "){" initializer_list ",}"

argument_expression_list_opt     : argument_expression_list?

argument_expression_list     : assignment_expression
                             | argument_expression_list "," assignment_expression

unary_expression     : postfix_expression
                     | "++" unary_expression
                     | "--" unary_expression
                     | unary_operator cast_expression
                     | "sizeof" unary_expression
                     | "sizeof(" type_name ")"
                     | "_Alignof(" type_name ")"

unary_operator     : "&" | "*" | "+" | "-" | "~" | "!"

cast_expression     : unary_expression
                    | "(" type_name ")" cast_expression

multiplicative_expression     : cast_expression
                              | multiplicative_expression "*" cast_expression
                              | multiplicative_expression "/" cast_expression
                              | multiplicative_expression "%" cast_expression

additive_expression     : multiplicative_expression
                        | additive_expression "+" multiplicative_expression
                        | additive_expression "-" multiplicative_expression

shift_expression     : additive_expression
                     | shift_expression "<<" additive_expression
                     | shift_expression ">>" additive_expression

relational_expression     : shift_expression
                          | relational_expression "<" shift_expression
                          | relational_expression ">" shift_expression
                          | relational_expression "<=" shift_expression
                          | relational_expression ">=" shift_expression

equality_expression     : relational_expression
                        | equality_expression "==" relational_expression
                        | equality_expression "!=" relational_expression

and_expression     : equality_expression
                   | and_expression "&" equality_expression

exclusive_or_expression     : and_expression
                            | exclusive_or_expression "^" and_expression

inclusive_or_expression     : exclusive_or_expression
                            | inclusive_or_expression "|" exclusive_or_expression

logical_and_expression     : inclusive_or_expression
                           | logical_and_expression "&&" inclusive_or_expression

logical_or_expression     : logical_and_expression
                          | logical_or_expression "||" logical_and_expression

conditional_expression     : logical_or_expression
                           | logical_or_expression "?" expression ":" conditional_expression

assignment_expression_opt     : assignment_expression?

assignment_expression     : conditional_expression
                          | unary_expression assignment_operator assignment_expression

assignment_operator     : "=" | "*=" | "/=" | "%=" | "+=" | "-=" | "<<=" | ">>=" | "&=" | "^=" | "|="

expression_opt     : expression?

expression     : assignment_expression
               | expression "," assignment_expression

constant_expression     : conditional_expression

//  A.2.2 Declarations

declaration     : declaration_specifiers init_declarator_list_opt ";"
                | static_assert_declaration

declaration_specifiers_opt     : declaration_specifiers?

declaration_specifiers     : storage_class_specifier declaration_specifiers_opt
                           | type_specifier declaration_specifiers_opt
                           | type_qualifier declaration_specifiers_opt
                           | function_specifier declaration_specifiers_opt
                           | alignment_specifier declaration_specifiers_opt

init_declarator_list_opt     : init_declarator_list?

init_declarator_list     : init_declarator
                         | init_declarator_list "," init_declarator

init_declarator     : declarator
                    | declarator "=" initializer

storage_class_specifier     : "typedef" | "extern" | "static" | "_Thread_local" | "auto" | "register"

type_specifier     : "void" | "char" | "short" | "int" | "long" | "float" | "double" | "signed" | "unsigned" | "_Bool"
                   | "_Complex" | atomic_type_specifier | struct_or_union_specifier | enum_specifier | typedef_name

struct_or_union_specifier     : struct_or_union identifier_opt "{" struct_declaration_list "}"
                              | struct_or_union identifier

struct_or_union     : "struct" | "union"

struct_declaration_list     : struct_declaration
                            | struct_declaration_list struct_declaration

struct_declaration     : specifier_qualifier_list struct_declarator_list_opt ";"
                       | static_assert_declaration

specifier_qualifier_list_opt     : specifier_qualifier_list?

specifier_qualifier_list     : type_specifier specifier_qualifier_list_opt
                             | type_qualifier specifier_qualifier_list_opt

struct_declarator_list_opt     : struct_declarator_list?

struct_declarator_list     : struct_declarator
                           | struct_declarator_list "," struct_declarator

struct_declarator     : declarator
                      | declarator_opt ":" constant_expression

enum_specifier     : "enum" identifier_opt "{" enumerator_list "}"
                   | "enum" identifier_opt "{" enumerator_list ",}"
                   | "enum" identifier

enumerator_list     : enumerator
                    | enumerator_list "," enumerator

enumerator     : enumeration_constant
               | enumeration_constant "=" constant_expression

atomic_type_specifier     : "_Atomic(" type_name ")"

type_qualifier     : "const" | "restrict" | "volatile" | "_Atomic"

function_specifier     : "inline" | "_Noreturn"

alignment_specifier     : "_Alignas(" type_name ")"
                        | "_Alignas(" constant_expression ")"

declarator_opt     : declarator?

declarator     : pointer_opt direct_declarator

direct_declarator     : identifier
                      | "(" declarator ")"
                      | direct_declarator "[" type_qualifier_list_opt assignment_expression_opt "]"
                      | direct_declarator "[static" type_qualifier_list_opt assignment_expression "]"
                      | direct_declarator "[" type_qualifier_list "static" assignment_expression "]"
                      | direct_declarator "[" type_qualifier_list_opt "*]"
                      | direct_declarator "(" parameter_type_list ")"
                      | direct_declarator "(" identifier_list_opt ")"

pointer_opt     : pointer?

pointer     : "*" type_qualifier_list_opt
            | "*" type_qualifier_list_opt pointer

type_qualifier_list_opt     : type_qualifier_list?

type_qualifier_list     : type_qualifier
                        | type_qualifier_list type_qualifier

parameter_type_list_opt     : parameter_type_list?

parameter_type_list     : parameter_list
                        | parameter_list ",..."

parameter_list     : parameter_declaration
                   | parameter_list "," parameter_declaration

parameter_declaration     : declaration_specifiers declarator
                          | declaration_specifiers abstract_declarator_opt

identifier_list_opt     : identifier_list?

identifier_list     : identifier
                    | identifier_list "," identifier

type_name     : specifier_qualifier_list abstract_declarator_opt

abstract_declarator_opt     : abstract_declarator?

abstract_declarator     : pointer
                        | pointer_opt direct_abstract_declarator

direct_abstract_declarator_opt     : direct_abstract_declarator?

direct_abstract_declarator     : "(" abstract_declarator ")"
                               | direct_abstract_declarator_opt "[" type_qualifier_list_opt assignment_expression_opt "]"
                               | direct_abstract_declarator_opt "[static" type_qualifier_list_opt assignment_expression "]"
                               | direct_abstract_declarator_opt "[" type_qualifier_list "static" assignment_expression "]"
                               | direct_abstract_declarator_opt "[*]"
                               | direct_abstract_declarator_opt "(" parameter_type_list_opt ")"

typedef_name     : identifier

initializer     : assignment_expression
                | "{" initializer_list "}"
                | "{" initializer_list ",}"

initializer_list     : designation_opt initializer
                     | initializer_list "," designation_opt initializer

designation_opt     : designation?

designation     : designator_list "="

designator_list     : designator
                    | designator_list designator

designator     : "[" constant_expression "]"
               | "." identifier

static_assert_declaration     : "_Static_assert(" constant_expression "," string_literal ");"

//  A.2.3 Statements

statement     : labeled_statement
              | compound_statement
              | expression_statement
              | selection_statement
              | iteration_statement
              | jump_statement

labeled_statement     : identifier ":" statement
                      | "case" constant_expression ":" statement
                      | "default:" statement

compound_statement     : "{" block_item_list_opt "}"

block_item_list_opt     : block_item_list?

block_item_list     : block_item
                    | block_item_list block_item

block_item     : declaration
               | statement

expression_statement     : expression_opt ";"

selection_statement     : "if(" expression ")" statement
                        | "if(" expression ")" statement "else" statement
                        | "switch(" expression ")" statement

iteration_statement     : "while(" expression ")" statement
                        | "do" statement "while(" expression ");"
                        | "for(" expression_opt ";" expression_opt ";" expression_opt ")" statement
                        | "for(" declaration expression_opt ";" expression_opt ")" statement

jump_statement     : "goto" identifier ";"
                   | "continue;"
                   | "break;"
                   | "return" expression_opt ";"

//  A.2.4 External definitions

translation_unit     : external_declaration
                     | translation_unit external_declaration

external_declaration     : function_definition
                         | declaration

function_definition     : declaration_specifiers declarator declaration_list_opt compound_statement

declaration_list_opt     : declaration_list?

declaration_list     : declaration
                     | declaration_list declaration

//  A.3 Preprocessing directives

preprocessing_file     : group_opt

group_opt     : group?

group     : group_part
          | group group_part

group_part     : if_section
               | control_line
               | text_line
               | "#" non_directive

if_section     : if_group elif_groups_opt else_group_opt endif_line

if_group     : "#if" constant_expression new_line group_opt
             | "#ifdef" identifier new_line group_opt
             | "#ifndef" identifier new_line group_opt

elif_groups_opt     : elif_groups?

elif_groups     : elif_group
                | elif_groups elif_group

elif_group     : "#elif" constant_expression new_line group_opt

else_group_opt     : else_group?

else_group     : "#else" new_line group_opt

endif_line     : "#endif" new_line

control_line     : "#include" pp_tokens new_line
                 | "#define" identifier replacement_list new_line
                 | "#define" identifier lparen identifier_list_opt ")" replacement_list new_line
                 | "#define" identifier lparen "...)" replacement_list new_line
                 | "#define" identifier lparen identifier_list ",...)" replacement_list new_line
                 | "#undef" identifier new_line
                 | "#line" pp_tokens new_line
                 | "#error" pp_tokens_opt new_line
                 | "#pragma" pp_tokens_opt new_line
                 | "#" new_line

text_line     : pp_tokens_opt new_line

non_directive     : pp_tokens new_line

lparen     : "("

replacement_list     : pp_tokens_opt

pp_tokens_opt     : pp_tokens?

pp_tokens     : preprocessing_token
              | pp_tokens preprocessing_token

new_line     : "\r\n" | "\n"
